plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'com.diffplug.spotless' version '5.6.1'
}

group = 'org.arkecosystem'
version = '2.0.0-beta-1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    integration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationImplementation.extendsFrom implementation
    integrationRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.8.1'
    implementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.9.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

    integrationImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    integrationImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    integrationImplementation 'org.hamcrest:hamcrest-library:2.2'
    integrationImplementation 'org.arkecosystem:crypto:2.0.0-beta-1'
    integrationCompileOnly 'org.slf4j:slf4j-api:1.7.32'
    integrationRuntimeOnly 'org.slf4j:slf4j-simple:1.7.32'
    integrationImplementation 'com.github.rholder:guava-retrying:2.0.0'
    integrationImplementation  "cash.z.ecc.android:kotlin-bip39:1.0.2"
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

spotless {
    java {
        target fileTree(projectDir) {
            include 'src/main/**/*.java'
            include 'src/test/**/*.java'
            exclude '**/build/**'
        }
        googleJavaFormat('1.1').aosp()
        removeUnusedImports()
    }
}

task formatCode(dependsOn: ['spotlessApply'])

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'standalone'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

build.dependsOn 'spotlessApply'

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()
}

check.dependsOn integrationTest

wrapper {
    gradleVersion = '7.2'
}

task writeNewPom {
    doLast {
        pom {
        }.writeTo("$buildDir/newpom.xml")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                groupId = project.group
                version = project.version
                artifactId = 'client'

                name = 'java-client'
                description = 'A Simple REST API Client Implementation in Java for the ARK CORE Blockchain Framework.'
                url = 'https://sdk.ark.dev/java/client'
                inceptionYear = '2018'

                licenses {
                    license {
                        name = 'MIT'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        name = 'Kovač Žan'
                        email = 'zan@ark.io'
                        organization = 'ARK Ecosystem'
                        organizationUrl = 'https://ark.io'
                    }
                    developer {
                        name = 'Kristjan Košič'
                        email = 'kristjan@ark.io'
                        organization = 'ARK Ecosystem'
                        organizationUrl = 'https://ark.io'
                    }
                    developer {
                        name = 'Brian Faust'
                        email = 'brian@ark.io'
                        organization = 'ARK Ecosystem'
                        organizationUrl = 'https://ark.io'
                    }
                    developer {
                        name = 'Joshua Noack'
                        email = 'joshua@ark.io'
                        organization = 'ARK Ecosystem'
                        organizationUrl = 'https://ark.io'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/ArkEcosystem/java-client.git'
                    developerConnection = 'scm:git:ssh://github.com:ArkEcosystem/java-client.git'
                    url = 'https://github.com/ArkEcosystem/java-client'
                }
            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey as String, signingPassword as String)
    sign publishing.publications.mavenJava
}
